_root.装备生命周期函数.挂载披风初始化 = function(反射对象, 参数对象) 
{
    参数对象 = 参数对象 || {};
    参数对象.type = "身体_引用";  // 设定物件类型
    参数对象.object = 参数对象.cloak || "将军披风";  // 设定物件种类

    _root.装备生命周期函数.挂载背景物件初始化(反射对象, 参数对象);
};

_root.装备生命周期函数.挂载披风周期 = function(反射对象, 参数对象) 
{
    _root.装备生命周期函数.挂载背景物件周期(反射对象, 参数对象);
};


_root.装备生命周期函数.挂载背景物件初始化 = function(反射对象, 参数对象)
{
    var 自机 = 反射对象.自机;
    var 搭载层 = 自机.底层背景;
    var 物件类型 = 参数对象.type ? 参数对象.type : "身体_引用";
    var 物件种类 = 参数对象.object ? 参数对象.object : "将军披风";
    var 物件标签 = 反射对象.标签名 + 物件种类;
    var 物件;

    if(自机[物件标签])
    {
        物件 = 自机[物件标签];
        _root.服务器.发布服务器消息("读取缓存 " + _root.常用工具函数.对象转JSON(物件));
    }
    else
    {
        物件 = 搭载层.attachMovie(物件种类, 物件标签, 搭载层.getNextHighestDepth());
        自机[物件标签] = 物件;
        _root.服务器.发布服务器消息("创建物件 " + _root.常用工具函数.对象转JSON(物件));
    }

    反射对象.搭载层 = 搭载层;
    反射对象.物件 = 物件;
    反射对象.物件类型 = 物件类型;
    反射对象.物件种类 = 物件种类;
    反射对象.物件标签 = 物件标签;

    _root.常用工具函数.设置卸载回调(自机, function() {
        物件.removeMovieClip();
    });    // 使用设置卸载回调函数
    var 卸载对象 = {动作:function(额外参数){
                        额外参数.物件.removeMovieClip();
                   },
                   额外参数:{物件:物件}
    };

    反射对象.生命周期函数列表.push(卸载对象);
};

_root.装备生命周期函数.挂载背景物件周期 = function(反射对象, 参数对象)
{
    var 物件 = 反射对象.物件;
    var 自机 = 反射对象.自机;
    var 引用 = 自机[反射对象.物件类型];

    if(自机.hp <= 0 || ((反射对象.是否为主角 ? _root[反射对象.装备类型] : 反射对象.自机[反射对象.装备类型]) !== 反射对象.装备名称) || !引用)
    {
        _root.装备生命周期函数.移除周期函数(反射对象);
        自机[反射对象.物件标签] = null;
        /*
        if(引用)
        {
            _root.add2map3(物件,2);
        }
        */
        物件.removeMovieClip();
        return;
    }

    var 全局坐标点 = new Object();
    全局坐标点.x = 引用._x;
    全局坐标点.y = 引用._y;
    引用.localToGlobal(全局坐标点);

    var 局部坐标点 = new Object();
    局部坐标点.x = 全局坐标点.x;
    局部坐标点.y = 全局坐标点.y;
    反射对象.搭载层.globalToLocal(局部坐标点);

    物件._x = 局部坐标点.x;
    物件._y = 局部坐标点.y;

    物件._rotation = 引用._rotation + 引用._parent._rotation + 引用._parent._parent._rotation;
};
